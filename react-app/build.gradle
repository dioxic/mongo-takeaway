
plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'base'

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

node {
    version = '11.10.0'
    npmVersion = '6.7.0'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    //workDir = file("${project.buildDir}/nodejs")
    //npmWorkDir = file("${project.buildDir}/npm")
    //nodeModulesDir = file("${project.projectDir}/src/main/app")
}

npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'build'
}

// pack output of the build into JAR file
task packageNpmApp(type: Zip) {
    dependsOn npm_run_build
    baseName 'react-app'
    extension 'jar'
    destinationDir file("${projectDir}/package")
    from('build') {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into 'static'
    }
}

// declare a dedicated scope for publishing the packaged JAR
configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

// expose the artifact created by the packaging task
artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type 'jar'
    }
}

assemble.dependsOn packageNpmApp

clean {
    delete packageNpmApp.archivePath
}